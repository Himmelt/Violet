plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'org.soraworld'
version = '2.1.1'
archivesBaseName = 'Violet'

sourceCompatibility = targetCompatibility = 1.8
compileJava {
    sourceCompatibility = targetCompatibility = 1.8
}

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/himmelt/Minecraft"
    }
    maven {
        name = 'spigot'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}

configurations {
    shade
    compile.extendsFrom(shade)
}

dependencies {
    shade 'org.soraworld:hocon:1.0.3'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

javadoc {
    options.encoding = 'UTF-8'
    options.locale = 'en'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    configurations.shade.copyRecursive().setTransitive(false).each {
        artifact -> from(zipTree(artifact))
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'org.soraworld'
            artifactId = 'violet'
            version = project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    key = BINTRAY_KEY
    user = BINTRAY_USER
    publications = ['mavenJava']
    pkg {
        repo = 'Minecraft'
        name = 'Violet'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Himmelt/Violet.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'Violet Plugin Library.'
            released = new Date()
        }
    }
}
